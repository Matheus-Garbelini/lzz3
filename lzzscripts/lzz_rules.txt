#================================================================================
#
# Lazy C++ Grammar
#
#================================================================================

%keyword AUTO      "auto"
%keyword BOOL      "bool"
%keyword CATCH     "catch"
%keyword CHAR      "char"
%keyword CLASS     "class"
%keyword CONST     "const"
%keyword DELETE    "delete"
%keyword DINIT     "_dinit"
%keyword DLL_API   "dll_api"
%keyword DOUBLE    "double"
%keyword ENUM      "enum"
%keyword EXPLICIT  "explicit"
%keyword EXTERN    "extern"
%keyword FLOAT     "float"
%keyword FRIEND    "friend"
%keyword INLINE    "inline"
%keyword INT       "int"
%keyword LONG      "long"
%keyword MUTABLE   "mutable"
%keyword NAMESPACE "namespace"
%keyword NEW       "new"
%keyword OPERATOR  "operator"
%keyword PRIVATE   "private"
%keyword PROTECTED "protected"
%keyword PUBLIC    "public"
%keyword REGISTER  "register"
%keyword SHORT     "short"
%keyword SIGNED    "signed"
%keyword STATIC    "static"
%keyword STRUCT    "struct"
%keyword TEMPLATE  "template"
%keyword THROW     "throw"
%keyword TRY       "try"
%keyword TYPEDEF   "typedef"
%keyword TYPENAME  "typename"
%keyword UNION     "union"
%keyword UNSIGNED  "unsigned"
%keyword USING     "using"
%keyword VIRTUAL   "virtual"
%keyword VOID      "void"
%keyword VOLATILE  "volatile"
%keyword WCHAR     "wchar"

#================================================================================
#
# start
#
#================================================================================

start -> decl-seq-opt

decl-seq-opt -> decl-seq
decl-seq-opt ->
decl-seq -> decl-seq decl
decl-seq -> decl

decl -> namespace
decl -> simple-decl
decl -> fun-def
decl -> class-decl
decl -> class-def
decl -> tmpl-decl
decl -> enum-def
decl -> lazy-class
decl -> functor-def
decl -> tmpl-inst 
decl -> using-decl
decl -> using-dir

#================================================================================
#
# declaration specifiers
#
#================================================================================

# cv spec
cv-spec -> cv-token

cv-token -> CONST
cv-token -> VOLATILE

# cv spec seq
v_decl_spec_seq_1: cv-spec-seq -> cv-spec
v_decl_spec_seq_2: cv-spec-seq -> cv-spec-seq cv-spec

cv-spec-seq-opt -> cv-spec-seq
cv-spec-seq-opt ->

# ftor (function and storage) spec
ftor-spec -> ftor-token

ftor-token -> INLINE
ftor-token -> VIRTUAL
ftor-token -> EXPLICIT
ftor-token -> STATIC
ftor-token -> EXTERN
ftor-token -> MUTABLE
ftor-token -> AUTO
ftor-token -> REGISTER
ftor-token -> DLL_API

# builtin type
builtin-type -> builtin-type-token

builtin-type-token -> BOOL
builtin-type-token -> CHAR
builtin-type-token -> DOUBLE
builtin-type-token -> FLOAT
builtin-type-token -> INT
builtin-type-token -> LONG
builtin-type-token -> SHORT
builtin-type-token -> SIGNED
builtin-type-token -> UNSIGNED
builtin-type-token -> VOID
builtin-type-token -> WCHAR

# elaborated type
elab_type: elab-type -> class-key obj-name
elab_type: elab-type -> ENUM obj-name
elab_type: elab-type -> TYPENAME obj-name

class-key -> CLASS
class-key -> STRUCT
class-key -> UNION

null-decl-spec-seq ->

# xxxS - ftor
s_decl_spec_seq_1: xxxS-decl-spec-seq -> ftor-spec
s_decl_spec_seq_2: xxxS-decl-spec-seq -> xxxS-decl-spec-seq ftor-spec
# xxVx - cv
v_decl_spec_seq_1: xxVx-decl-spec-seq -> cv-spec
v_decl_spec_seq_2: xxVx-decl-spec-seq -> xxVx-decl-spec-seq cv-spec
# xxVS - ftor cv
s_decl_spec_seq_2: xxVS-decl-spec-seq -> xxVx-decl-spec-seq ftor-spec
v_decl_spec_seq_2: xxVS-decl-spec-seq -> xxxS-decl-spec-seq cv-spec
s_decl_spec_seq_2: xxVS-decl-spec-seq -> xxVS-decl-spec-seq ftor-spec
v_decl_spec_seq_2: xxVS-decl-spec-seq -> xxVS-decl-spec-seq cv-spec

# xBVx - builtin cv-opt
b_decl_spec_seq_1: xBVx-decl-spec-seq -> builtin-type
b_decl_spec_seq_2: xBVx-decl-spec-seq -> xxVx-decl-spec-seq builtin-type
v_decl_spec_seq_2: xBVx-decl-spec-seq -> xBVx-decl-spec-seq cv-spec
b_decl_spec_seq_2: xBVx-decl-spec-seq -> xBVx-decl-spec-seq builtin-type
# xBVS - builtin ftor cv-opt
s_decl_spec_seq_2: xBVS-decl-spec-seq -> xBVx-decl-spec-seq ftor-spec
b_decl_spec_seq_2: xBVS-decl-spec-seq -> xxxS-decl-spec-seq builtin-type
b_decl_spec_seq_2: xBVS-decl-spec-seq -> xxVS-decl-spec-seq builtin-type
s_decl_spec_seq_2: xBVS-decl-spec-seq -> xBVS-decl-spec-seq ftor-spec
v_decl_spec_seq_2: xBVS-decl-spec-seq -> xBVS-decl-spec-seq cv-spec
b_decl_spec_seq_2: xBVS-decl-spec-seq -> xBVS-decl-spec-seq builtin-type
# xUxx - user cv-opt
u_decl_spec_seq_1: xUVx-decl-spec-seq -> obj-name
u_decl_spec_seq_2: xUVx-decl-spec-seq -> xxVx-decl-spec-seq obj-name
v_decl_spec_seq_2: xUVx-decl-spec-seq -> xUVx-decl-spec-seq cv-spec
# xUVS - user ftor cv-opt
s_decl_spec_seq_2: xUVS-decl-spec-seq -> xUVx-decl-spec-seq ftor-spec
u_decl_spec_seq_2: xUVS-decl-spec-seq -> xxxS-decl-spec-seq obj-name
u_decl_spec_seq_2: xUVS-decl-spec-seq -> xxVS-decl-spec-seq obj-name
s_decl_spec_seq_2: xUVS-decl-spec-seq -> xUVS-decl-spec-seq ftor-spec
v_decl_spec_seq_2: xUVS-decl-spec-seq -> xUVS-decl-spec-seq cv-spec
# xExx - elab-type
e_decl_spec_seq_1: xExx-decl-spec-seq -> elab-type
# xEVx - elab-type cv
v_decl_spec_seq_2: xEVx-decl-spec-seq -> xExx-decl-spec-seq cv-spec
e_decl_spec_seq_2: xEVx-decl-spec-seq -> xxVx-decl-spec-seq elab-type
v_decl_spec_seq_2: xEVx-decl-spec-seq -> xEVx-decl-spec-seq cv-spec
# xExS - elab-type ftor
s_decl_spec_seq_2: xExS-decl-spec-seq -> xExx-decl-spec-seq ftor-spec
e_decl_spec_seq_2: xExS-decl-spec-seq -> xxxS-decl-spec-seq elab-type
s_decl_spec_seq_2: xExS-decl-spec-seq -> xExS-decl-spec-seq ftor-spec
# xEVS - elab-type cv ftor
s_decl_spec_seq_2: xEVS-decl-spec-seq -> xEVx-decl-spec-seq ftor-spec
v_decl_spec_seq_2: xEVS-decl-spec-seq -> xExS-decl-spec-seq cv-spec
e_decl_spec_seq_2: xEVS-decl-spec-seq -> xxVS-decl-spec-seq elab-type
s_decl_spec_seq_2: xEVS-decl-spec-seq -> xEVS-decl-spec-seq ftor-spec
v_decl_spec_seq_2: xEVS-decl-spec-seq -> xEVS-decl-spec-seq cv-spec

# Txxx - typedef
t_decl_spec_seq_1: Txxx-decl-spec-seq -> TYPEDEF
# TxxS - typedef cv
v_decl_spec_seq_2: TxVx-decl-spec-seq -> Txxx-decl-spec-seq cv-spec
t_decl_spec_seq_2: TxVx-decl-spec-seq -> xxVx-decl-spec-seq TYPEDEF
v_decl_spec_seq_2: TxVx-decl-spec-seq -> TxVx-decl-spec-seq cv-spec
# TxxS - typedef ftor
s_decl_spec_seq_2: TxxS-decl-spec-seq -> Txxx-decl-spec-seq ftor-spec
t_decl_spec_seq_2: TxxS-decl-spec-seq -> xxxS-decl-spec-seq TYPEDEF
s_decl_spec_seq_2: TxxS-decl-spec-seq -> TxxS-decl-spec-seq ftor-spec
# TxxS - typedef cv ftor
s_decl_spec_seq_2: TxVS-decl-spec-seq -> TxVx-decl-spec-seq ftor-spec
v_decl_spec_seq_2: TxVS-decl-spec-seq -> TxxS-decl-spec-seq cv-spec
t_decl_spec_seq_2: TxVS-decl-spec-seq -> xxVS-decl-spec-seq TYPEDEF
s_decl_spec_seq_2: TxVS-decl-spec-seq -> TxVS-decl-spec-seq ftor-spec
v_decl_spec_seq_2: TxVS-decl-spec-seq -> TxVS-decl-spec-seq cv-spec
# TBVx - typedef builtin cv-opt
b_decl_spec_seq_2: TBVx-decl-spec-seq -> Txxx-decl-spec-seq builtin-type
b_decl_spec_seq_2: TBVx-decl-spec-seq -> TxVx-decl-spec-seq builtin-type
t_decl_spec_seq_2: TBVx-decl-spec-seq -> xBVx-decl-spec-seq TYPEDEF
v_decl_spec_seq_2: TBVx-decl-spec-seq -> TBVx-decl-spec-seq cv-spec
b_decl_spec_seq_2: TBVx-decl-spec-seq -> TBVx-decl-spec-seq builtin-type
# TBVS - typedef builtin ftor cv-opt (can really have ftor on typedef?)
s_decl_spec_seq_2: TBVS-decl-spec-seq -> TBVx-decl-spec-seq ftor-spec
b_decl_spec_seq_2: TBVS-decl-spec-seq -> TxxS-decl-spec-seq builtin-type
b_decl_spec_seq_2: TBVS-decl-spec-seq -> TxVS-decl-spec-seq builtin-type
t_decl_spec_seq_2: TBVS-decl-spec-seq -> xBVS-decl-spec-seq TYPEDEF
s_decl_spec_seq_2: TBVS-decl-spec-seq -> TBVS-decl-spec-seq ftor-spec
v_decl_spec_seq_2: TBVS-decl-spec-seq -> TBVS-decl-spec-seq cv-spec
b_decl_spec_seq_2: TBVS-decl-spec-seq -> TBVS-decl-spec-seq builtin-type
# TUVx - typedef user cv-opt
u_decl_spec_seq_2: TUVx-decl-spec-seq -> Txxx-decl-spec-seq obj-name
u_decl_spec_seq_2: TUVx-decl-spec-seq -> TxVx-decl-spec-seq obj-name
t_decl_spec_seq_2: TUVx-decl-spec-seq -> xUVx-decl-spec-seq TYPEDEF
v_decl_spec_seq_2: TUVx-decl-spec-seq -> TUVx-decl-spec-seq cv-spec
# TUVx - typedef user ftor cv-opt
s_decl_spec_seq_2: TUVS-decl-spec-seq -> TUVx-decl-spec-seq ftor-spec
u_decl_spec_seq_2: TUVS-decl-spec-seq -> TxxS-decl-spec-seq obj-name
u_decl_spec_seq_2: TUVS-decl-spec-seq -> TxVS-decl-spec-seq obj-name
t_decl_spec_seq_2: TUVS-decl-spec-seq -> xUVS-decl-spec-seq TYPEDEF
s_decl_spec_seq_2: TUVS-decl-spec-seq -> TUVS-decl-spec-seq ftor-spec
v_decl_spec_seq_2: TUVS-decl-spec-seq -> TUVS-decl-spec-seq cv-spec
# TEVx - typedef elab-type cv-opt
e_decl_spec_seq_2: TEVx-decl-spec-seq -> Txxx-decl-spec-seq elab-type
e_decl_spec_seq_2: TEVx-decl-spec-seq -> TxVx-decl-spec-seq elab-type
t_decl_spec_seq_2: TEVx-decl-spec-seq -> xExx-decl-spec-seq TYPEDEF
t_decl_spec_seq_2: TEVx-decl-spec-seq -> xEVx-decl-spec-seq TYPEDEF
v_decl_spec_seq_2: TEVx-decl-spec-seq -> TEVx-decl-spec-seq cv-spec
# TEVS - typedef elab-type cv-opt ftor
s_decl_spec_seq_2: TEVS-decl-spec-seq -> TEVx-decl-spec-seq ftor-spec
e_decl_spec_seq_2: TEVS-decl-spec-seq -> TxxS-decl-spec-seq elab-type
e_decl_spec_seq_2: TEVS-decl-spec-seq -> TxVS-decl-spec-seq elab-type
t_decl_spec_seq_2: TEVS-decl-spec-seq -> xExS-decl-spec-seq TYPEDEF
t_decl_spec_seq_2: TEVS-decl-spec-seq -> xEVS-decl-spec-seq TYPEDEF
s_decl_spec_seq_2: TEVS-decl-spec-seq -> TEVS-decl-spec-seq ftor-spec
v_decl_spec_seq_2: TEVS-decl-spec-seq -> TEVS-decl-spec-seq cv-spec

# Fxxx - friend
f_decl_spec_seq_1: Fxxx-decl-spec-seq -> FRIEND
# FxxS - friend cv
v_decl_spec_seq_2: FxVx-decl-spec-seq -> Fxxx-decl-spec-seq cv-spec
f_decl_spec_seq_2: FxVx-decl-spec-seq -> xxVx-decl-spec-seq FRIEND
v_decl_spec_seq_2: FxVx-decl-spec-seq -> FxVx-decl-spec-seq cv-spec
# FxxS - friend ftor
s_decl_spec_seq_2: FxxS-decl-spec-seq -> Fxxx-decl-spec-seq ftor-spec
f_decl_spec_seq_2: FxxS-decl-spec-seq -> xxxS-decl-spec-seq FRIEND
s_decl_spec_seq_2: FxxS-decl-spec-seq -> FxxS-decl-spec-seq ftor-spec
# FxxS - friend cv ftor
s_decl_spec_seq_2: FxVS-decl-spec-seq -> FxVx-decl-spec-seq ftor-spec
v_decl_spec_seq_2: FxVS-decl-spec-seq -> FxxS-decl-spec-seq cv-spec
f_decl_spec_seq_2: FxVS-decl-spec-seq -> xxVS-decl-spec-seq FRIEND
s_decl_spec_seq_2: FxVS-decl-spec-seq -> FxVS-decl-spec-seq ftor-spec
v_decl_spec_seq_2: FxVS-decl-spec-seq -> FxVS-decl-spec-seq cv-spec
# FBVS - friend builtin cv-opt ftor-opt
s_decl_spec_seq_2: FBVS-decl-spec-seq -> Fxxx-decl-spec-seq builtin-type
s_decl_spec_seq_2: FBVS-decl-spec-seq -> FxVx-decl-spec-seq builtin-type
s_decl_spec_seq_2: FBVS-decl-spec-seq -> FxxS-decl-spec-seq builtin-type
s_decl_spec_seq_2: FBVS-decl-spec-seq -> FxVS-decl-spec-seq builtin-type
s_decl_spec_seq_2: FBVS-decl-spec-seq -> xBVx-decl-spec-seq FRIEND
s_decl_spec_seq_2: FBVS-decl-spec-seq -> xBVS-decl-spec-seq FRIEND
s_decl_spec_seq_2: FBVS-decl-spec-seq -> FBVS-decl-spec-seq ftor-spec
b_decl_spec_seq_2: FBVS-decl-spec-seq -> FBVS-decl-spec-seq cv-spec
b_decl_spec_seq_2: FBVS-decl-spec-seq -> FBVS-decl-spec-seq builtin-type
# FUVS - friend user cv-opt ftor-opt
u_decl_spec_seq_2: FUVS-decl-spec-seq -> Fxxx-decl-spec-seq obj-name
u_decl_spec_seq_2: FUVS-decl-spec-seq -> FxVx-decl-spec-seq obj-name
u_decl_spec_seq_2: FUVS-decl-spec-seq -> FxxS-decl-spec-seq obj-name
u_decl_spec_seq_2: FUVS-decl-spec-seq -> FxVS-decl-spec-seq obj-name
f_decl_spec_seq_2: FUVS-decl-spec-seq -> xUVx-decl-spec-seq FRIEND
f_decl_spec_seq_2: FUVS-decl-spec-seq -> xUVS-decl-spec-seq FRIEND
s_decl_spec_seq_2: FUVS-decl-spec-seq -> FUVS-decl-spec-seq ftor-spec
v_decl_spec_seq_2: FUVS-decl-spec-seq -> FUVS-decl-spec-seq cv-spec
# FExx - fried elab-type
e_decl_spec_seq_2: FExx-decl-spec-seq -> Fxxx-decl-spec-seq elab-type
f_decl_spec_seq_2: FExx-decl-spec-seq -> xExx-decl-spec-seq FRIEND
# FEVS - friend elab-type cv-opt ftor-opt
e_decl_spec_seq_2: FEVS-decl-spec-seq -> FxVx-decl-spec-seq elab-type
e_decl_spec_seq_2: FEVS-decl-spec-seq -> FxxS-decl-spec-seq elab-type
e_decl_spec_seq_2: FEVS-decl-spec-seq -> FxVS-decl-spec-seq elab-type
f_decl_spec_seq_2: FEVS-decl-spec-seq -> xExS-decl-spec-seq FRIEND
f_decl_spec_seq_2: FEVS-decl-spec-seq -> xEVx-decl-spec-seq FRIEND
f_decl_spec_seq_2: FEVS-decl-spec-seq -> xEVS-decl-spec-seq FRIEND
s_decl_spec_seq_2: FEVS-decl-spec-seq -> FEVS-decl-spec-seq ftor-spec
v_decl_spec_seq_2: FEVS-decl-spec-seq -> FEVS-decl-spec-seq cv-spec
FEVS-decl-spec-seq -> FExx-decl-spec-seq

#================================================================================
#
# declaration
#
#================================================================================

# object
decl: obj-decl -> xBVx-decl-spec-seq obj-dcl
decl: obj-decl -> xBVS-decl-spec-seq obj-dcl
decl: obj-decl -> xUVx-decl-spec-seq obj-dcl
decl: obj-decl -> xUVS-decl-spec-seq obj-dcl
decl: obj-decl -> xExx-decl-spec-seq obj-dcl
decl: obj-decl -> xEVx-decl-spec-seq obj-dcl
decl: obj-decl -> xExS-decl-spec-seq obj-dcl
decl: obj-decl -> xEVS-decl-spec-seq obj-dcl

# function declaration
decl: fun-decl -> null-decl-spec-seq fun-dcl
decl: fun-decl -> xxxS-decl-spec-seq fun-dcl
decl: fun-decl -> xBVx-decl-spec-seq fun-dcl
decl: fun-decl -> xBVS-decl-spec-seq fun-dcl
decl: fun-decl -> xUVx-decl-spec-seq >+ fun-dcl
decl: fun-decl -> xUVS-decl-spec-seq >+ fun-dcl
decl: fun-decl -> xExx-decl-spec-seq fun-dcl
decl: fun-decl -> xEVx-decl-spec-seq fun-dcl
decl: fun-decl -> xExS-decl-spec-seq fun-dcl
decl: fun-decl -> xEVS-decl-spec-seq fun-dcl

# friend function declaration
decl: fun-decl -> Fxxx-decl-spec-seq fun-dcl
decl: fun-decl -> FxxS-decl-spec-seq fun-dcl
decl: fun-decl -> FBVS-decl-spec-seq fun-dcl
decl: fun-decl -> FUVS-decl-spec-seq >+ fun-dcl
decl: fun-decl -> FEVS-decl-spec-seq fun-dcl

# functor declaration
decl: functor-decl -> xBVx-decl-spec-seq functor-dcl
decl: functor-decl -> xBVS-decl-spec-seq functor-dcl
decl: functor-decl -> xUVx-decl-spec-seq functor-dcl
decl: functor-decl -> xUVS-decl-spec-seq functor-dcl
decl: functor-decl -> xExx-decl-spec-seq functor-dcl
decl: functor-decl -> xEVx-decl-spec-seq functor-dcl
decl: functor-decl -> xExS-decl-spec-seq functor-dcl
decl: functor-decl -> xEVS-decl-spec-seq functor-dcl

# typedef
decl: tdef-decl -> TBVx-decl-spec-seq param-dcl
decl: tdef-decl -> TBVS-decl-spec-seq param-dcl
decl: tdef-decl -> TUVx-decl-spec-seq param-dcl
decl: tdef-decl -> TUVS-decl-spec-seq param-dcl
decl: tdef-decl -> TEVx-decl-spec-seq param-dcl
decl: tdef-decl -> TEVS-decl-spec-seq param-dcl

# abstract declaration
decl: abstract-decl < -> xBVx-decl-spec-seq abstract-dcl-opt
decl: abstract-decl < -> xUVx-decl-spec-seq abstract-dcl-opt
decl: abstract-decl < -> xExx-decl-spec-seq abstract-dcl-opt
decl: abstract-decl < -> xEVx-decl-spec-seq abstract-dcl-opt

# parameter declaration
decl: param-decl -> xBVx-decl-spec-seq param-dcl
decl: param-decl -> xUVx-decl-spec-seq param-dcl
decl: param-decl -> xExx-decl-spec-seq param-dcl
decl: param-decl -> xEVx-decl-spec-seq param-dcl

#================================================================================
#
# object declarator
#
#================================================================================

obj-dcl -> obj-a-dcl

ptr_dcl: obj-a-dcl -> ptr-oper obj-a-dcl
ptr_dcl: obj-b-dcl -> ptr-oper obj-a-dcl

obj-a-dcl -> obj-a-direct-dcl
obj-b-dcl -> obj-b-direct-dcl

ptr_oper_1: ptr-oper -> TIMES cv-spec-seq-opt
ptr_oper_2: ptr-oper -> BITAND
ptr_oper_3: ptr-oper -> obj-name DCOLON TIMES cv-spec-seq-opt

obj-a-direct-dcl -> obj-b-direct-dcl
obj-a-direct-dcl -> obj-dcl-id

direct_dcl_1: obj-b-direct-dcl -> obj-b-direct-dcl param-decl-1-body > RPAREN cv-spec-seq-opt throw-spec-opt
direct_dcl_2: obj-b-direct-dcl -> obj-a-direct-dcl LBRACK block-opt 5 RBRACK
direct_dcl_3: obj-b-direct-dcl -> LPAREN obj-b-dcl RPAREN

direct_dcl_3: obj-dcl-id -> LPAREN obj-dcl-id RPAREN
obj-dcl-id -> obj-id

id: obj-id -> obj-name

# throw spec
throw-spec-opt ->
throw-spec-opt -> throw-spec

throw_spec: throw-spec -> THROW LPAREN type-id-list-opt RPAREN

# type id
type-id-list-opt -> 
type-id-list-opt -> type-id-list

type_id_list_1: type-id-list -> type-id
type_id_list_2: type-id-list -> type-id-list COMMA type-id

type_id: type-id -> abstract-decl

block-opt ->
block-opt -> block

block -> BLOCK 

#================================================================================
#
# abstract declarator
#
#================================================================================

abstract-dcl-opt -> abstract-dcl
abstract-dcl-opt ->

ptr_dcl: abstract-dcl -> ptr-oper abstract-dcl-opt
abstract-dcl -> abstract-direct-dcl

direct_dcl_1: abstract-direct-dcl -> abstract-direct-dcl-opt param-decl-1-body RPAREN cv-spec-seq-opt throw-spec-opt
direct_dcl_2: abstract-direct-dcl -> abstract-direct-dcl-opt LBRACK block-opt 5 RBRACK
# on LPAREN try nested abstract declarator before parameter decl body
direct_dcl_3: abstract-direct-dcl -> LPAREN > abstract-dcl RPAREN
        
abstract-direct-dcl-opt -> abstract-direct-dcl
abstract-direct-dcl-opt ->

#================================================================================
#
# parameter declarator
#
#================================================================================

param-dcl -> param-a-dcl

ptr_dcl: param-a-dcl -> ptr-oper param-a-dcl
ptr_dcl: param-b-dcl -> ptr-oper param-a-dcl

param-a-dcl -> param-a-direct-dcl
param-b-dcl -> param-b-direct-dcl

param-a-direct-dcl -> param-b-direct-dcl
param-a-direct-dcl -> obj-dcl-id

direct_dcl_1: param-b-direct-dcl -> param-a-direct-dcl param-decl-1-body RPAREN cv-spec-seq-opt throw-spec-opt
direct_dcl_2: param-b-direct-dcl -> param-a-direct-dcl LBRACK block-opt 5 RBRACK
direct_dcl_3: param-b-direct-dcl -> LPAREN param-b-dcl RPAREN

#================================================================================
#
# parameter declaration body 
#
#================================================================================

# param decl body 1
param_decl_body_1: param-decl-1-body -> param-decl-1-list ellipse-opt
param_decl_body_2: param-decl-1-body -> param-decl-1-list COMMA ELLIPSE
param_decl_body_3: param-decl-1-body -> LPAREN ellipse-opt
param_decl_body_4: param-decl-1-body -> LPAREN VOID

# param decl body 2, after semi-colon in functor 
param_decl_body_1: param-decl-2-body -> param-decl-2-list ellipse-opt
param_decl_body_2: param-decl-2-body -> param-decl-2-list COMMA ELLIPSE
param_decl_body_3: param-decl-2-body -> SEMI ellipse-opt
param_decl_body_4: param-decl-2-body -> SEMI VOID

# accept on first param declaration, don't backtrack and parse LPAREN as start of direct initializer
param_decl_list_1: param-decl-1-list <* -> LPAREN > param-init-decl
param_decl_list_2: param-decl-1-list -> param-decl-1-list COMMA param-init-decl
param_decl_list_1: param-decl-2-list -> SEMI param-init-decl
param_decl_list_2: param-decl-2-list -> param-decl-2-list COMMA param-init-decl

ellipse-opt -> 
ellipse-opt -> ELLIPSE

# param init decl, block can end with ')' or ','
param_decl_1: param-init-decl <* -> param-decl
param_decl_2: param-init-decl <* -> param-decl ASSIGN block 4

# same semantics as above but block can end with '>' or ','
param_decl_1: tmpl-param-init-decl <* -> param-decl 
param_decl_2: tmpl-param-init-decl <* -> param-decl ASSIGN block 8

# nested name
nested_name_1: nested-name -> DCOLON
nested_name_2: nested-name -> obj-name DCOLON >>!
nested-name-opt -> nested-name
nested-name-opt -> 

# object name
name_1: obj-name -> nested-name-opt obj-base-name
name_2: obj-name -> nested-name-opt obj-base-name LT >! block-opt 1 GT
name_3: obj-name -> nested-name TEMPLATE obj-base-name LT >! block-opt 1 GT

# function name
name_1: fun-name -> nested-name-opt fun-base-name
name_2: fun-name -> nested-name-opt fun-base-name LT block-opt 1 GT

# base name
base_name_1: obj-base-name -> IDENT
base_name_2: fun-base-name -> BITNOT IDENT
base_name_3: fun-base-name -> OPERATOR oper
base_name_4: fun-base-name -> OPERATOR abstract-decl >

# operator
oper_1: oper -> LPAREN RPAREN
oper_2: oper -> LBRACK RBRACK
oper_3: oper -> NEW LBRACK RBRACK
oper_4: oper -> DELETE LBRACK RBRACK
oper_5: oper -> token-oper

token-oper -> AND
token-oper -> ASSIGN
token-oper -> BITAND
token-oper -> BITAND_ASSIGN
token-oper -> BITNOT
token-oper -> BITOR
token-oper -> BITOR_ASSIGN
token-oper -> COMMA
token-oper -> DEC
token-oper -> DELETE
token-oper -> DIVIDE
token-oper -> DIVIDE_ASSIGN
token-oper -> EQ
token-oper -> GT
token-oper -> GTEQ
token-oper -> INC
token-oper -> LSHIFT
token-oper -> LSHIFT_ASSIGN
token-oper -> LT
token-oper -> LTEQ
token-oper -> MINUS
token-oper -> MINUS_ASSIGN
token-oper -> MOD
token-oper -> MOD_ASSIGN
token-oper -> NEW
token-oper -> NOT
token-oper -> NOTEQ
token-oper -> OR
token-oper -> PLUS
token-oper -> PLUS_ASSIGN
token-oper -> PTR
token-oper -> PTR_TIMES
token-oper -> RSHIFT
token-oper -> RSHIFT_ASSIGN
token-oper -> TIMES
token-oper -> TIMES_ASSIGN
token-oper -> XOR
token-oper -> XOR_ASSIGN

#
# object initializer
#

obj-init-opt -> obj-init
obj-init-opt ->

obj_init_1: obj-init -> ASSIGN block 3
obj_init_2: obj-init -> DINIT LPAREN block 2 RPAREN
obj_init_3: obj-init -> LBRACE block 7 RBRACE

expr_list_1: expr-list -> block 4
expr_list_2: expr-list -> expr-list COMMA block 4

expr_list_opt: expr-list-opt -> expr-list
expr-list-opt ->

#
# function declarator 
#

fun_dcl: fun-dcl -> fun-ptr-dcl pure-opt

pure-opt -> pure
pure-opt ->

pure: pure -> ASSIGN ZERO

ptr_dcl: fun-ptr-dcl -> ptr-oper fun-ptr-dcl
fun-ptr-dcl -> fun-a-direct-dcl

direct_dcl_4: fun-a-direct-dcl -> obj-dcl-id param-decl-1-body RPAREN cv-spec-seq-opt throw-spec-opt
direct_dcl_4: fun-a-direct-dcl -> fun-dcl-id param-decl-1-body RPAREN cv-spec-seq-opt throw-spec-opt
direct_dcl_1: fun-a-direct-dcl -> fun-a-direct-dcl param-decl-1-body RPAREN cv-spec-seq-opt throw-spec-opt
direct_dcl_2: fun-a-direct-dcl -> fun-a-direct-dcl LBRACK block-opt 5 RBRACK
direct_dcl_3: fun-a-direct-dcl -> LPAREN fun-ptr-dcl RPAREN

# function id
fun-dcl-id -> fun-id
direct_dcl_3: fun-dcl-id -> LPAREN fun-dcl-id RPAREN
id: fun-id -> fun-name

#
# functor declarator
#

fun_dcl: functor-dcl -> functor-ptr-dcl pure-opt

ptr_dcl: functor-ptr-dcl -> ptr-oper functor-ptr-dcl
functor-ptr-dcl -> functor-direct-dcl

direct_dcl_5: functor-direct-dcl -> obj-dcl-id param-decl-1-body param-decl-2-body RPAREN cv-spec-seq-opt throw-spec-opt
direct_dcl_1: functor-direct-dcl -> functor-direct-dcl param-decl-1-body RPAREN cv-spec-seq-opt throw-spec-opt
direct_dcl_2: functor-direct-dcl -> functor-direct-dcl LBRACK block-opt 5 RBRACK
direct_dcl_3: functor-direct-dcl -> LPAREN functor-dcl RPAREN

#
# simple declaration
#
simple-decl * -> simple-a-decl SEMI

simple-a-decl -> simple-b-decl
simple-a-decl -> simple-c-decl

# object and function

simple_decl_1: simple-b-decl -> nested-obj-decl obj-init-opt
simple_decl_2: simple-b-decl -> nested-fun-decl

nested_decl: nested-obj-decl -> simple-b-decl COMMA obj-dcl
nested_decl: nested-fun-decl -> simple-b-decl COMMA fun-dcl

nested-obj-decl -> obj-decl
nested-fun-decl -> fun-decl

# typedef

simple_decl2: simple-c-decl *< -> nested-tdef-decl

nested_decl: nested-tdef-decl -> simple-c-decl COMMA param-dcl

nested-tdef-decl -> tdef-decl

#================================================================================
#
# namespace
#
#================================================================================

namespace= namespace * -> open-namespace decl-seq-opt RBRACE
open_namespace_1: open-namespace -> NAMESPACE obj-name LBRACE
open_namespace_2: open-namespace -> NAMESPACE LBRACE

#================================================================================
#
# function definition
#
#================================================================================

# try obj-name as user-name before obj-id
fun_def: fun-def * -> fun-decl * try-opt ctor-init-opt LBRACE block-opt 7 RBRACE handler-seq-opt

# ctor init
ctor-init-opt -> ctor-init
ctor-init-opt -> 

ctor_init: ctor-init -> COLON mbr-init-list
mbr_init_list_1: mbr-init-list -> mbr-init
mbr_init_list_2: mbr-init-list -> mbr-init-list COMMA mbr-init

mbr_init_1: mbr-init * -> obj-name LPAREN BLOCK 2 RPAREN
mbr_init_2: mbr-init * -> obj-name LBRACE BLOCK 7 RBRACE

# handler
try-opt -> TRY
try-opt ->

seq: handler-seq-opt -> handler-seq
handler-seq-opt ->

seq_1: handler-seq -> handler
seq_2: handler-seq -> handler-seq handler

handler: handler * -> CATCH LPAREN catch-decl RPAREN LBRACE block-opt 7 RBRACE

catch_decl_1: catch-decl -> param-decl
catch_decl_2: catch-decl -> ELLIPSE

#================================================================================
#
# class
#
#================================================================================

# declaration
class_decl: class-decl -> xExx-decl-spec-seq SEMI
class_decl: class-decl -> FExx-decl-spec-seq SEMI

# definition
class_def: class-def -> class-head <* LBRACE mbr-decl-seq-opt RBRACE semi-opt

class_head: class-head -> class-key obj-name base-clause-opt

semi-opt ->
semi-opt -> SEMI

base-clause-opt -> base-clause
base-clause-opt ->

base_clause: base-clause -> COLON base-spec-list

base_spec_list_1: base-spec-list -> base-spec
base_spec_list_2: base-spec-list -> base-spec-list COMMA base-spec

base_spec_1: base-spec * -> obj-name
base_spec_2: base-spec * -> VIRTUAL access-opt obj-name
base_spec_3: base-spec * -> access virtual-opt obj-name

access-opt -> 
access-opt -> access

access -> PUBLIC
access -> PROTECTED
access -> PRIVATE

virtual-opt -> VIRTUAL
virtual-opt ->

mbr-decl-seq-opt -> mbr-decl-seq
mbr-decl-seq-opt ->

mbr-decl-seq -> mbr-decl-seq mbr-decl
mbr-decl-seq -> mbr-decl

mbr-decl -> access-spec
mbr-decl -> simple-decl
mbr-decl -> fun-def semi-opt
mbr-decl -> class-decl
mbr-decl -> class-def
mbr-decl -> tmpl-decl
mbr-decl -> enum-def
mbr-decl -> lazy-class
mbr-decl -> functor-def
mbr-decl -> using-decl

access_spec: access-spec * -> access COLON

#================================================================================
#
# template declaration
#
#================================================================================

tmpl_decl: tmpl-decl * -> tmpl-spec tmpl-spec-decl

tmpl_spec: tmpl-spec * -> TEMPLATE LT tmpl-params GT

# tmpl params
tmpl_params: tmpl-params -> tmpl-param-list-opt

# tmpl param list opt
tmpl-param-list-opt -> tmpl-param-list
tmpl-param-list-opt -> 

# tmpl param list
tmpl_param_list_1: tmpl-param-list -> tmpl-param
tmpl_param_list_2: tmpl-param-list -> tmpl-param-list COMMA tmpl-param

# tmpl param
tmpl-param -> type-param
tmpl-param -> tmpl-param-init-decl
tmpl-param -> tmpl-tmpl-param

# type param
type_param_1: type-param <* -> type-key ++ obj-name
type_param_2: type-param <* -> type-key obj-name ASSIGN abstract-decl

# type key
type-key -> CLASS
type-key -> TYPENAME

# tmpl tmpl param
tmpl_tmpl_param_1: tmpl-tmpl-param <* -> TEMPLATE LT tmpl-params GT CLASS obj-name
tmpl_tmpl_param_2: tmpl-tmpl-param <* -> TEMPLATE LT tmpl-params GT CLASS obj-name ASSIGN obj-name

# tmpl spec decl
tmpl-spec-decl -> class-def
tmpl-spec-decl -> fun-def
tmpl-spec-decl -> lazy-class
tmpl-spec-decl -> functor-def

#tmpl-spec-decl -> simple-decl
#tmpl-spec-decl -> class-decl
#tmpl-spec-decl -> friend-class-decl
#tmpl-spec-decl -> tmpl-decl
#tmpl-spec-decl -> lazy-ctor
#tmpl-spec-decl -> functor-def
#tmpl-spec-decl -> simple-functor-decl

#================================================================================
#
# enum def
#
#================================================================================

enum_def: enum-def -> ENUM obj-name-opt LBRACE enum-body-opt RBRACE semi-opt

obj-name-opt -> obj-name
obj-name-opt ->

enum-body-opt -> enum-body
enum-body-opt ->

enum_body: enum-body -> enumtor-list comma-opt

comma-opt -> COMMA
comma-opt -> 

enumtor_list_1: enumtor-list -> enumtor-list COMMA enumtor
enumtor_list_2: enumtor-list -> enumtor

# enumtor decl
enumtor_1: enumtor < * -> obj-name
enumtor_2: enumtor < * -> obj-name ASSIGN BLOCK 9

#================================================================================
#
# lazy class
#
#================================================================================

lazy_class: lazy-class -> lazy-class-head LBRACE mbr-decl-seq-opt RBRACE semi-opt

lazy_class_head: lazy-class-head -> class-key obj-name param-decl-1-body RPAREN lazy-base-clause-opt

lazy-base-clause-opt -> lazy-base-clause
lazy-base-clause-opt ->

base_clause: lazy-base-clause -> COLON lazy-base-spec-list

base_spec_list_1: lazy-base-spec-list -> lazy-base-spec
base_spec_list_2: lazy-base-spec-list -> lazy-base-spec-list COMMA lazy-base-spec

lazy_base_spec: lazy-base-spec -> base-spec base-init-opt

base-init-opt -> 
base-init-opt -> base-init

# only support parens in base spec init list as LBRACE conflicts with class body
base_init: base-init -> LPAREN expr-list-opt RPAREN

# functor def
functor_def: functor-def -> functor-decl lazy-base-clause-opt try-opt LBRACE block-opt 7 RBRACE handler-seq-opt

#================================================================================
#
# explicit template instantiation
#
#================================================================================

tmpl_inst: tmpl-inst -> tmpl-inst-begin tmpl-inst-decl

tmpl_inst_begin: tmpl-inst-begin -> TEMPLATE

tmpl-inst-decl -> simple-decl
tmpl-inst-decl -> class-decl

#================================================================================
#
# using
#
#================================================================================

# declaration
using_decl: using-decl -> USING obj-name SEMI

# directive
using_dir: using-dir -> USING NAMESPACE obj-name SEMI
